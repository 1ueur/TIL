### 무조건 분기
무조건 분기를 사용하기 위해서는 ```JMP, CALL``` 명령어를 사용한다. 보통 고급 언어의 GOTO 문장과 같다.
JMP는 순차적 실행 절차와 같은데, 명령어 실행과 IP값 지정 절차가 다르다.

**JMP [address/offset]** ;특정 주소나 offset으로 점프한다.

분기되어 갈 곳의 주소는 IP에 저장되는데, 분기되어 갈 곳의 주소와 현재 명령어 주소의 차이로 결정된다. 
이 차이를 변위(d)라고 한다. <br/>
**변위(d) = 분기되어 갈 곳의 주소 - 현재 명령어 주소**

### 무조건 분기의 절차
1. BIU는 IP에 있는 내용을 주소 버스로 출력한다.
2. 주소가 가리키는 기억 장소에서 명령어를 인출하여 명령어 큐에 저장한다.
3. EU에서 명령어를 명령어 큐로부터 읽어와서 해독한다.
4. 분기되어 갈 주소(JMP 명령어의 연산 항에 있는 기호)와 현재 IP 값의 차이(변위 : d)를 계산한다.
5. IP = IP + d

<br/>

> **Q. 키보드로부터 입력 받은 문자 중 소문자를 대문자로 변환하는 프로그램** <br/>
> **기본 알고리즘** : 키보드로부터 입력받은 문자가 'a'보다 작거나 'z'보다 크면 그대로 출력하고 아니면 20H(32d)값을 빼주고 그 값에 해당하는 문자를 화면에 출력한다. <br/>
> 'a'의 ASCII 코드 값은 61H(97d)이고, 'A'의 ASCII 코드 값은 41H(65d)이다. <br/>
> **[키 입력 방법]** <br/>
> 키보드로부터 한 문자를 입력하려면 MS-DOS의 함수 호출 1번을 사용한다. <br/>

```assembly
MAIN SEGMENT
ASSUME CS:MAIN

L1 : MOV AH, 1 ;키보드로부터 한 문자 입력받는다.
INT 21H

CMP AL, 1AH ;비교 명령어, 1AH = '^z', ^z가 오면 FIN으로 이동한다.
JE FIN ;JE는 Equal

CMP AL, 'a' ;비교 명령어, a보다 작으면 L2로 이동한다.
JB L2 ;JB는 작으면
CMP AL, 'z' ;비교 명령어, z보다 크면 L2로 이동한다.
JA L2 ;JA는 크면

SUB AL, 'a'-'A' ;위 조건에 모두 해당하지 않으면 97-65=32만큼 빼준다.
L2 : MOV DL, AL ;L2는 그대로 출력 명령어
MOV AH, 2
INT 21H
JMP L1 ;다시 L1으로 돌아간다. (^z가 입력될 때까지)

FIN : MOV AH, 4CH ;종료
INT 21H

MAIN ENDS
END
```

